/*
 * Copyright 2008-present MongoDB, Inc.
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *   http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

configure(subprojects.findAll { it.name != 'util' }) {
    tasks.withType(Test) {

        afterSuite { suite, result ->
            if (suite != null && result != null && suite.parent == null) {
                def resultsSummary = """Tests summary:
                | ${result.testCount} tests,
                | ${result.successfulTestCount} succeeded,
                | ${result.failedTestCount} failed,
                | ${result.skippedTestCount} skipped""".stripMargin().replace("\n", "")

                def border = '=' * resultsSummary.size()
                logger.lifecycle("\n$border")
                logger.lifecycle("Test result: ${result.resultType}")
                logger.lifecycle(resultsSummary)
                logger.lifecycle("${border}\n")
            }
        }
    }
}